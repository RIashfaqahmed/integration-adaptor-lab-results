plugins {
	id 'org.springframework.boot' version '2.4.1'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'io.freefair.lombok' version '5.3.0'
	id 'application'
	id 'jacoco'
	id 'checkstyle'
	id 'com.github.spotbugs' version '4.6.0'
	id 'idea'
}

group = 'uk.nhs.digital.nhsconnect'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

mainClassName = 'uk.nhs.digital.nhsconnect.lab.results.IntegrationAdapterLabResultsApplication'

sourceSets {
	intTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	intTestImplementation.extendsFrom implementation
	intTestImplementation.extendsFrom testImplementation
	intTestRuntimeOnly.extendsFrom runtimeOnly
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.apache.httpcomponents:httpclient:4.5.13'
	implementation 'com.heroku.sdk:env-keystore:1.1.6'

	implementation 'ca.uhn.hapi.fhir:hapi-fhir-base:5.2.0'
	implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-dstu3:5.2.0'
	implementation 'org.springframework:spring-jms:5.3.3'
	implementation 'org.apache.qpid:qpid-jms-client:0.56.0'

	implementation 'org.projectlombok:lombok'
	testImplementation 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	testImplementation 'org.testcontainers:testcontainers:1.15.1'
	testImplementation 'org.awaitility:awaitility:4.0.3'
	testImplementation 'org.assertj:assertj-core:3.18.1'
}

lombok {
	config['lombok.log.fieldName'] = 'LOGGER'
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

task integrationTest(type: Test) {
	useJUnitPlatform() {
		description = 'Runs integration tests.'
		group = 'verification'

		testClassesDirs = sourceSets.intTest.output.classesDirs
		classpath = sourceSets.intTest.runtimeClasspath
		shouldRunAfter test
	}
}

check.dependsOn integrationTest

spotbugsTest.enabled = false
spotbugsIntTest.enabled = false
spotbugsMain {
	reports {
		html {
			enabled = false
		}
		xml {
			enabled = true
		}
	}
}

spotbugs {
	excludeFilter = file("${rootDir}/config/spotbugs/exclude.xml")
}

checkstyle {
	configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

idea {
	module {
		testSourceDirs += file('src/intTest/java')
	}
}
